/*
  This module defines the Base Ontology database - i.e. the
  fundamental ontological entities used by the rest of the ARF
  database system.
*/
:- module(base, []).
:- use_module(library(uuid)).
:- use_module(library(persistency)).

:- persistent
     entry(id:atom, version:nonneg),

     /*
       Accepts two arguments: id, a unique ID generated by uuid;
       and name; and asserts that there exists an entity named name.
     */
     entity(entry:atom, name:string),

     /*
       Accepts two arguments: id, a unique ID generated by uuid;
       and timestamp, a timestamp generated using get_time, and
       asserts that an event occured at timestamp.
     */
     event(entry:atom, timestamp:float),

     /*
       Accepts three arguments: id, a unique ID generated by uuid;
       of, an entry ID; and value, another entry ID; and asserts
       that the entry referenced by of has value as an attribute.
     */
     attribute(entry:atom, subject:atom),

     /*
       Accepts two arguments: event, an event ID; and by, an entity
       ID; and asserts that the entity referenced by by performed
       an action described by the event referenced by event.
     */
     action(event:atom, actor:atom),

     /*
       Accepts one argument: actionID, an actionID; and asserts
       that the referenced action represents an activity (an action
       spanning some duration).
     */
     activity(actionID:atom),

     /*
       Accepts three arguments: action, an action ID; of, an entity
       ID; unit, the measurement units; and value, the measurement
       value.
     */
     measurement(action:atom, of:atom, unit:atom, value:any),

     /*
       Accepts two arguments: measurement, a measurement ID; and
       precision, the precision of the measurement.
     */
     measurementQuality(measurement:atom, precision:any),

     duration(attributeID:atom),

     weight(attributeID:atom),

     circumference(attributeID:atom).

/*
  Accepts one argument: FileName, a string that represents a file
  name; and opens the referenced file as a Base Ontology database.
*/
attachBaseDB(FileName) :- db_attach(FileName, []).

entryCreate(ID) :-
  uuid(ID),
  assert_entry(ID, 0).

entityCreate(Name, ID) :-
  entryCreate(ID),
  assert_entity(ID, Name).

eventCreate(ID) :-
  entryCreate(ID),
  get_time(Timestamp),
  assert_event(ID, Timestamp).

eventCreate(Timestamp, ID) :-
  entryCreate(ID),
  assert_event(ID, Timestamp).

attributeCreate(SubjectID, ID) :-
  entryCreate(ID),
  assert_attribute(ID, SubjectID).

actionCreate(ActorID, EventID) :-
  eventCreate(EventID),
  assert_action(EventID, ActorID).

actionEventID(Action, EventID) :- Action = action(EventID, _).

actionActorID(Action, ActorID) :- Action = action(_, ActorID).

measurementCreate(SourceID, OfID, Unit, Value, ID) :-
  actionCreate(SourceID, ID),
  assert_measurement(ID, OfID, Unit, Value).

measurementCreate(SourceID, OfID, Unit, Value, Precision, ID) :-
  actionCreate(SourceID, ID),
  assert_measurement(ID, OfID, Unit, Value),
  assert_measurementQuality(ID, Precision).

durationCreate(SourceID, EventID, Unit, Value, ID) :-
  attributeCreate(EventID, ID),
  assert_duration(ID),
  measurementCreate(SourceID, ID, Unit, Value, _).

activityCreate(ActorID, Unit, Duration, ID) :-
  base:entity(ActorID, "me"), !,
  actionCreate(ActorID, ID),
  assert_activity(ID),
  durationCreate(ActorID, ID, Unit, Duration, _).

/*
  Accepts one argument; action, an action term; and returns the
  time at which the action occured/began.
*/
actionTimestamp(action(EventID, _), Timestamp) :- event(EventID, Timestamp).
